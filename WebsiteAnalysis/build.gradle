buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "java"
    id "application"

    id "jacoco"
    id "pmd"
    id "com.github.spotbugs" version "5.0.9"
}

java {
    sourceCompatibility = JavaVersion.toVersion(11)
    targetCompatibility = JavaVersion.toVersion(11)
}

repositories {
    mavenCentral()
}

dependencies {
    // Everything in src/main (production code).
    // implementation ...
    implementation "org.jsoup:jsoup:1.16.1"
    implementation "org.apache.poi:poi:5.2.3+"
    implementation "org.apache.poi:poi-ooxml:5.2.3+"
    implementation "com.cedarsoftware:json-io:4.14.0"

    // Everything in src/test (test code)
    testImplementation "org.junit.jupiter:junit-jupiter:5.8.2"
    testImplementation "org.hamcrest:hamcrest-library:2.2"
}

jar {
    archiveBaseName = "Website_Analysis"

    manifest {
        attributes(
            "Main-Class": "edu.odu.cs.cs350.Driver"
        )
    }
}

application {
    mainClass = "edu.odu.cs.cs350.Driver"
}

tasks.withType(Test) {
    useJUnitPlatform()

    reports {
        html.required = true
    }
    ignoreFailures = true

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

test {
    finalizedBy jacocoTestReport
}

//------------------------------------------------------------------------------
// Documentation Tool(s)
//------------------------------------------------------------------------------
javadoc {
    failOnError false
}

pmd {
    toolVersion = "6.21.0"
    ignoreFailures = true
    ruleSets = [
        "category/java/bestpractices.xml",
        "category/java/codestyle.xml",
        "category/java/design.xml",
        "category/java/errorprone.xml",
        "category/java/performance.xml"
    ]
}

pmdTest {
    enabled = false
}

spotbugsMain {
    ignoreFailures = true
    effort = "max"
    reportLevel = "medium"

    reports {
        xml.enabled = true
        html.enabled = true
    }
}

spotbugsTest {
    enabled = false
}
